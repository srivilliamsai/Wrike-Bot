
form_data = form.get("values");
type = form_data.get("type");
if(type == "configure")
{
	tasklist_id = form_data.get("tasklist_id").get("value");
	query = Map();
	query.put("criteria","userid=" + user.get("id"));
	db_query = zoho.cliq.getRecords("wrikedb",query);
	record_id = db_query.get("list").get(0).get("id");
	values = Map();
	values.put("tasklistid",tasklist_id);
	db = zoho.cliq.updateRecord("wrikedb",record_id,values);
	if(db.get("status").equalsIgnoreCase("SUCCESS"))
	{
		id = "Tasks";
		dividerElement = {"type":"divider"};
		footer = {"text":""};
		footer_text = "";
		editDefaultProject = {"label":"Edit Default Project","emotion":"positive","type":"invoke.function","name":"configureprojectform","id":id};
		tabsList = {{"label":"Project","id":"Project"},{"label":"Tasks","id":"Tasks"},{"label":"Schedule","id":"Schedule"},{"label":"Files","id":"Files"},{"label":"Documents","id":"Documents"}};
		account_id = db_query.get("list").get(0).get("accountid");
		project_id = db_query.get("list").get(0).get("projectid");
		if(tasklist_id != 0)
		{
			button = List();
			button.add(editDefaultProject);
			button.add({"label":"Edit Default Task List","type":"invoke.function","name":"configuretasklistform","emotion":"positive","id":"configuration"});
			header = {"title":"Tasks (page 1)","navigation":"new","buttons":button};
			tasks_url = "https://www.wrike.com/api/v4/folders/" + tasklist_id + "/tasks";
			tasks = invokeurl
			[
				url :tasks_url
				type :GET
				connection:"wrikeconnection"
			];
			info tasks;
			if(tasks.size() == 0)
			{
				sections = List();
				elements = List();
				elements.add({"type":"activity","title":"Task List Empty! üìù","description":"You don't have any Tasks yet!\nUse `/wrike Create a Task` in any Cliq chat or use the message action (select a message, click more, and then click `Create a Task`) to create your first task and start organizing your day! üìÖüí™","image_url":"https://i.postimg.cc/Wzq58K7J/wrikelogo.png"});
				elements.add(dividerElement);
				sections.add({"id":1,"elements":elements});
				return {"type":"applet","header":header,"footer":footer,"sections":sections,"tabs":tabsList,"active_tab":id};
				//return {"type":"applet","data_type":"info","tabs":tabsList,"info":{"title":" You don‚Äôt have any tasks yet.","description":"Click here to Create.","image_url":"https://i.postimg.cc/Wzq58K7J/wrikelogo.png"},"active_tab":id};
			}
			current_task = tasks.get(0);
			sections = List();
			elements = List();
			task_title = current_task.get("title");
			if(task_title.length() >= 50)
			{
				task_title = task_title.subString(0,47) + "..";
			}
			creator_name = current_task.get("creator").get("name");
			if(creator_name.length() >= 20)
			{
				creator_name = creator_name.subString(0,18) + "..";
			}
			description = "";
			if(current_task.get("description") != null)
			{
				description = current_task.get("description");
			}
			description = description.replaceAll("&lt;","<").replaceAll("&gt;",">").replaceAll("&amp;","&");
			description = description.replaceAll("<br>","\n");
			description = description.replaceAll("<[^>]*>","");
			description = description.trim();
			starts_on = current_task.get("starts_on");
			if(starts_on == null)
			{
				starts_on = "-";
			}
			starts_on = starts_on.replaceAll("T"," ").subString(0,starts_on.lastIndexOf(":"));
			due_on = current_task.get("due_on");
			if(due_on == null)
			{
				due_on = "-";
			}
			due_on = due_on.replaceAll("T"," ").subString(0,due_on.lastIndexOf(":"));
			created_at = current_task.get("created_at");
			if(created_at == null)
			{
				created_at = "-";
			}
			created_at = created_at.replaceAll("T"," ").subString(0,created_at.lastIndexOf(":"));
			completed_emoji = "‚úñÔ∏è";
			if(current_task.get("completed") == true)
			{
				completed_emoji = "‚úîÔ∏è";
			}
			elements.add({"type":"title","text":task_title});
			elements.add({"type":"text","text":description});
			elements.add({"type":"buttons","buttons":{{"label":"View Comments","emotion":"negative","type":"invoke.function","name":"viewcomments","id":id + "|" + current_task.get("comments_url") + "|1"},{"label":"View","emotion":"positive","type":"open.url","url":current_task.get("app_url")}}});
			elements.add(dividerElement);
			assignees_name = "";
			assignees = current_task.get("assignees");
			info assignees;
			for each  assignee in assignees
			{
				assignees_name = assignees_name + " " + assignee.get("name") + ",";
			}
			if(assignees_name.length() >= 1)
			{
				assignees_name = assignees_name.substring(0,assignees_name.length() - 1);
			}
			elements.add({"type":"fields","data":{{"Completed":completed_emoji},{"Starts on":starts_on},{"Due on":due_on},{"Assignees":assignees_name}},"style":{"short":false}});
			view_people_button = {"label":creator_name,"type":"invoke.function","name":"viewpeople","id":id + "|" + current_task.get("creator").get("id"),"emotion":"positive"};
			elements.add({"type":"text","text":"Created by " + " [View Profile]($1) " + " on " + created_at,"button_references":{"1":view_people_button}});
			elements.add(dividerElement);
			sections.add({"id":1,"elements":elements});
			i = 0;
			dataList = list();
			for each  task in tasks
			{
				i = i + 1;
				if(i == 1)
				{
					// skip current_entry
					continue;
				}
				title = task.get("title");
				if(title.length() >= 50)
				{
					title = task.subString(0,47) + "..";
				}
				description = "";
				if(task.get("description") != null)
				{
					description = task.get("description");
				}
				description = description.replaceAll("&lt;","<").replaceAll("&gt;",">").replaceAll("&amp;","&");
				description = description.replaceAll("<br>","\n");
				description = description.replaceAll("<[^>]*>","");
				description = description.trim();
				eachData = Map();
				eachData.put("title",title);
				eachData.put("description",description);
				eachData.put("image_url","https://i.imgur.com/McUuEPk.png");
				eachData.put("icon_url","https://i.postimg.cc/Wzq58K7J/wrikelogo.png");
				buttonList = {{"label":"Open","type":"invoke.function","name":"viewtasks","id":task.get("url") + "|" + tasks_url + "|1","emotion":"negative"}};
				eachData.put("buttons",buttonList);
				dataList.add(eachData);
				if(i == 15)
				{
					break;
				}
			}
			if(dataList.size() != 0)
			{
				elements = list();
				elements.add({"type":"title","text":"Other Tasks"});
				elements.add({"type":"cards","data":dataList,"style":{"view":"gallery","size":"small"}});
				elements.add({"type":"divider"});
				sections.add({"id":2,"elements":elements});
			}
			next_tasks = invokeurl
			[
				url :tasks_url + "?page=2"
				type :GET
				connection:"wrikeconnection"
			];
			info next_tasks;
			if(next_tasks.size() != 0)
			{
				footer = {"text":footer_text,"buttons":{{"label":"Next Page","emotion":"positive","type":"invoke.function","name":"viewtasks","id":"next_page" + "|" + tasks_url + "|2"}}};
				return {"type":"applet","header":header,"footer":footer,"sections":sections,"tabs":tabsList,"active_tab":id};
			}
			else
			{
				// no footer
				return {"type":"applet","header":header,"sections":sections,"tabs":tabsList,"active_tab":id};
			}
		}
		return {"type":"applet","data_type":"info","tabs":tabsList,"info":{"title":"Your default Task List is not configured yet!","description":"Click here to configure.","image_url":"https://i.postimg.cc/Wzq58K7J/wrikelogo.png","button":{"label":"Configure Default Task List","type":"invoke.function","name":"configuretasklistform","emotion":"positive","id":"configuration"}},"active_tab":id};
	}
	else
	{
		elements = list();
		elements.add({"type":"title","text":"‚ö†Ô∏è Oops! Something went wrong. üòî Please try again later"});
		sections = list();
		sections.add({"id":1,"elements":elements});
		header = {"title":"Error","navigation":"continue"};
		return {"type":"applet","header":header,"sections":sections,"active_tab":"Project"};
	}
	return Map();
}
info form_data;
info form_data.get("tasks_url");
if(form_data.get("tasks_url") == null)
{
	info "error banner";
	banner = {"text":"The selected project lacks a Task list. Create one with \"/wrike create a Task List\" to proceed.","status":"failure","type":"banner"};
	return banner;
}
tasks_url = form_data.get("tasks_url").get("value").replaceAll("-","/");
tasklist_name = form_data.get("tasks_url").get("label");
name = form_data.get("name").replaceAll("\n"," ").replaceAll("\t"," ").replaceAll("\r"," ");
description = form_data.get("description").replaceAll("\n","<br>").replaceAll("\t"," ").replaceAll("\r"," ");
assignees = form_data.get("assignees");
assignee_ids = List();
for each  assignee in assignees
{
	assignee_ids.add(assignee.get("value"));
}
due_on = form_data.get("due_on");
parameters = Map();
parameters.put("content",name);
parameters.put("description",description);
parameters.put("assignee_ids",assignee_ids);
parameters.put("due_on",due_on);
headers = Map();
headers.put("Content-Type","application/json");
tasks = invokeurl
[
	url :tasks_url
	type :POST
	parameters:toString(parameters)
	headers:headers
	connection:"wrikeconnection"
];
info tasks;
if(tasks.containskey("app_url"))
{
	app_url = tasks.get("app_url");
	response = Map();
	bot = Map();
	bot.put("name","Wrike Bot");
	bot.put("image","https://i.postimg.cc/Wzq58K7J/wrikelogo.png");
	response.put("bot",bot);
	card = Map();
	card.put("title","[Task](" + tasks.get("parent").get("app_url") + ")");
	card.put("theme","modern-inline");
	card.put("icon","https://i.postimg.cc/Wzq58K7J/wrikelogo.png");
	card.put("thumbnail","https://i.postimg.cc/Wzq58K7J/wrikelogo.png");
	response.put("card",card);
	buttonsList = list();
	buttonsList0 = Map();
	buttonsList0.put("label","View Task");
	//buttonsList0.put("type","+");
	action = Map();
	action.put("type","open.url");
	data = Map();
	data.put("web",app_url);
	action.put("data",data);
	buttonsList0.put("action",action);
	buttonsList.add(buttonsList0);
	//function button
	buttonsList1 = Map();
	buttonsList1.put("label","More Details");
	buttonsList1.put("type","+");
	action = Map();
	action.put("type","invoke.function");
	data = Map();
	data.put("name","viewcontent");
	action.put("data",data);
	buttonsList1.put("action",action);
	buttonsList1.put("key","tasks_url|" + tasks.get("url"));
	buttonsList.add(buttonsList1);
	response.put("buttons",buttonsList);
	query = Map();
	query.put("criteria","userid=" + user.get("id"));
	db = zoho.cliq.getRecords("wrikedb",query);
	account_id = db.get("list").get(0).get("accountid");
	project_url = "https://www.wrike.com/open.htm?id=" + tasks.get("folder").get("id");
	response.put("text","Hey [" + user.get("first_name") + "](zohoid:" + user.get("zoho_user_id") + ") üëã \nProject Name: [" + tasks.get("folder").get("name") + "](" + project_url + ") \n\nYou've successfully created a Task : " + name + " within " + tasklist_name + ". Keep up the great work!");
	return response;
}
else
{
	response = Map();
	bot = Map();
	bot.put("name","Wrike Bot");
	bot.put("image","https://i.postimg.cc/Wzq58K7J/wrikelogo.png");
	response.put("bot",bot);
	card = Map();
	card.put("theme","modern-inline");
	response.put("card",card);
	response.put("text","‚ö†Ô∏è Oops! Something went wrong. üòî Please try again later");
	return response;
}
return Map();
